import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage
import org.gradle.internal.jvm.Jvm

repositories {
    mavenCentral()
}

buildscript {
    repositories {
        gradlePluginPortal()
    }
    dependencies {
        classpath 'com.bmuschko:gradle-docker-plugin:9.3.1'
    }
}

allprojects {
    apply plugin: 'eclipse'

    group = 'com.anjlab'
    version = '3.3.0'
}

apply plugin: 'java'

def javaVersion = Jvm.current().javaVersion

sourceCompatibility = javaVersion.java8Compatible ? javaVersion : 1.7
targetCompatibility = sourceCompatibility

println "Building for Java ${targetCompatibility}"

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

dependencies {
    testImplementation 'junit:junit:4.13.1'
    testImplementation 'org.apache.derby:derby:10.14.2.0'
    testImplementation 'org.postgresql:postgresql:42.6.0'

    if (!project.hasProperty("jdbcDriver")) {
        ext.jdbcDriver = 'org.postgresql:postgresql:42.6.0'
    }

    implementation jdbcDriver

    implementation 'net.sf.opencsv:opencsv:2.3'
    implementation 'commons-cli:commons-cli:1.2'
    implementation 'commons-io:commons-io:2.7'
    implementation 'com.google.code.gson:gson:2.8.9'
    implementation 'org.apache.commons:commons-lang3:3.1'
    implementation 'io.dropwizard.metrics:metrics-core:3.1.2'
    implementation 'org.apache.commons:commons-compress:1.21'

    implementation 'commons-codec:commons-codec:1.13'
    implementation 'joda-time:joda-time:2.9.9'

    implementation 'org.openjdk.nashorn:nashorn-core:15.4'
}

tasks.register('downloadJdbcDriver')

classes.dependsOn downloadJdbcDriver

tasks.register('copyDeps', Copy) {
    into "$buildDir/libs/lib"
    from configurations.runtimeClasspath
}

tasks.register('copyShellScripts', Copy) {
    into "$buildDir/libs"
    from 'src/main/shell'
}

jar { dependsOn copyDeps, copyShellScripts }


apply plugin: 'com.bmuschko.docker-remote-api'

tasks.register('dockerBuildImage', DockerBuildImage) {
    inputDir = projectDir
    images.addAll(['anjlab/csv2db:latest', 'anjlab/csv2db:' + version])
}
